; Bowcraft 1.1 for ZuluHotel Omega
;
; 1. Make sure you have a separate bag containing a lot of shafts and feathers
;    in your bank.
; 2. Start the script, wait until the necessary containers are open and target
;    the bag container the shafts and feathers.
;
; This automatically takes 1 of each of your stack of shafts and feathers, and
; creates a single arrow. Setup is as such to prevent accidents when creating
; arrows based on the wrong pile of shafts, as arrows are created in bulk.

; Skillgain is smooth until about 90, use different wood colours for shafts
; after that.

gosub reset_backpack

gosub set_main_container
set %main_container_id #result
event SysMessage Opening main container.
nextcpos 600 500
gosub open_container %main_container_id

gosub set_resource_container
set %resource_container_id #result
event SysMessage Opening resource container.
nextcpos 600 300
gosub open_container %resource_container_id

start:

;feathers
finditem VLK C_, %resource_container_id ; Only fetch feathers from resource container.
gosub drag_from_stack #FINDID 1
set %feather #LLIFTEDID

;shafts
finditem SLK C_, %resource_container_id ; Only fetch shafts from resource container.
gosub drag_from_stack #FINDID 1
set %shaft #LLIFTEDID

gosub craft_arrow %feather %shaft

wait .5s

goto start

sub drag_from_stack
{
  set %stack %1
  event drag %stack
  wait 2
  msg %2 $
  click 450 350 P
  wait 2
  return
}

sub reset_backpack
{
  event SysMessage Resetting backpack. Please don't move it while the script is running.
  event macro 9 7
  nextcpos 400 300
  event macro 8 7
  wait 1s
  return
}

; No longer used, but leaving it here for now.
sub open_bank
{
  event SysMessage Opening bank and grabbing its ID.
  nextcpos 600 500
  msg bank $
  wait 1s
  return #CONTID
}

sub set_main_container
{
  event SysMessage Please target the container with the resource bag containing shafts and feathers.
  set #TARGCURS 1
  while #TARGCURS = 1
    wait 2
  return #LTARGETID
}

sub set_resource_container
{
  event SysMessage Please target the resource bag containing the shafts and feathers.
  set #TARGCURS 1
  while #TARGCURS = 1
    wait 2
  return #LTARGETID
}

sub craft_arrow
{
  set #LOBJECTID %2 ; Shaft
  wait 10
  event macro 17 0
  target 3s
  set #LTARGETID %1 ; Feather
  event macro 22 0
  while #CONTSIZEX <> 296 ; The size of the Bowcraft gump.
  {
    wait 1
  }
  return
}

sub open_container
{
  set #LOBJECTID %1
  while #CONTID <> %1
  {
    event macro 17 0
    wait 20
  }
  return
}
